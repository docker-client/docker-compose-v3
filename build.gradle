import java.text.SimpleDateFormat

buildscript {
    ext {
        junit_platform_version = '1.0.1'
        kotlin_version = '1.1.51'
    }

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_version"
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.8'
}

apply plugin: 'org.junit.platform.gradle.plugin'

apply plugin: 'kotlin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'de.gesellix'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
    bintrayDryRun = false
}

repositories {
    mavenLocal()
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile 'io.github.microutils:kotlin-logging:1.4.6'
    compile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'

    compile 'org.yaml:snakeyaml:1.18'
    compile 'com.squareup.moshi:moshi:1.5.0'
    compile 'com.squareup.moshi:moshi-kotlin:1.5.0'
    testCompile 'com.beust:klaxon:0.32'

    compile 'com.google.re2j:re2j:1.1'
//    compile 'com.github.fge:json-schema-validator:2.2.6'

    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile("org.jetbrains.spek:spek-api:1.1.5") {
//    testCompile("org.jetbrains.spek:spek-api:$kotlin_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime("org.jetbrains.spek:spek-junit-platform-engine:1.1.5") {
//    testRuntime("org.jetbrains.spek:spek-junit-platform-engine:$kotlin_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime "org.junit.platform:junit-platform-launcher:$junit_platform_version"
}

def dependencyVersions = [
        "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
        "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
        'org.slf4j:slf4j-api:1.7.25'
]

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force dependencyVersions
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        dockerCompose(MavenPublication) {
            artifactId 'docker-compose'
            version artifactVersion
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER ?: project["bintray.user"]
    key = System.env.BINTRAY_API_KEY ?: project["bintray.key"]
    publications = ['dockerCompose']
    pkg {
        repo = 'docker-utils'
        name = 'docker-compose'
        desc = 'A Docker compose v3 abstraction for the JVM'
        licenses = ['Apache-2.0']
        labels = ['docker', 'compose', 'stack', 'deploy', 'java']
        version = [
                name: artifactVersion
        ]
        vcsUrl = 'https://github.com/docker-client/docker-compose-v3.git'
    }
    dryRun = bintrayDryRun
}
bintrayUpload.dependsOn build

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
    distributionType = Wrapper.DistributionType.ALL
}
